"Setup controller-gen repositories and rules"

load("@aspect_bazel_lib//lib:repo_utils.bzl", "repo_utils")

# Platform names follow the platform naming convention in @aspect_bazel_lib//lib:repo_utils.bzl
CONTROLLER_GEN_PLATFORMS = {
    "darwin_amd64": struct(
        compatible_with = [
            "@platforms//os:macos",
            "@platforms//cpu:x86_64",
        ],
    ),
    "darwin_arm64": struct(
        compatible_with = [
            "@platforms//os:macos",
            "@platforms//cpu:aarch64",
        ],
    ),
    "linux_amd64": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:x86_64",
        ],
    ),
    "linux_arm64": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:aarch64",
        ],
    ),
    "linux_ppc64le": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:ppc",
        ],
    ),
    "linux_s390x": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:s390x",
        ],
    ),
    "windows_amd64": struct(
        compatible_with = [
            "@platforms//os:windows",
            "@platforms//cpu:x86_64",
        ],
    ),
}

DEFAULT_CONTROLLER_GEN_VERSION = "0.17.3"

# TODO: generate from GitHub releases
CONTROLLER_GEN_VERSIONS = {
    "0.17.2": {
        "darwin_amd64": "sha384-8ZIH983FEpJBnasG4C6j9NSdCHOTRsVT2Y/5tDwoIIYrD3tIm2iPw9Wovr4vV1v2",
        "darwin_arm64": "sha384-pSX6hbJLkGxFVIqYYkyKvr543MWIh9ZII0o1wqW5xZE/ZGXfzsRR0sTb+QdoWivw",
        "linux_amd64": "sha384-3AzbJs3A85PGPP3uhQHEneza7w3hW+x3w2RyFVBkesBe41MrKbehoR2zHG93EUim",
        "linux_arm64": "sha384-j+hLD1ixY+cqsGTSytoocOoH3VtUUUztglGcONiRxst4Yr8L/bzgAMdI9bIu60qw",
        "linux_ppc64le": "sha384-W9Tesu0f+xIKhTtZbMhJMwwKLzAhRB9Fey31Ig12TdIaua8lXaJ0bWDbLPAJCgO2",
        "linux_s390x": "sha384-20oylTV6LvD3lE1WrtNlEw7upCylVU76GF6LcpsE8UubHE2wCQ7DnE4hh6oWDdpO",
        "windows_amd64": "sha384-0xlCi+r7yMGUHr6h58BxZAQDtbhzM5SW2ZRAiBOqrfiLJxixnvU1vbgL9K8Ez5+u",
    },
    "0.17.3": {
        "darwin_amd64": "sha384-pOrIi2qQw4zB6kYHjgcUExzxil8ao5JOrbrcNRClcEKDq/GsCz4IlPLmeCQT43yy",
        "darwin_arm64": "sha384-P5QaN4bmBnzjHZQliVfP3Gs3e5UwOP8gYCZDne5DvOCnOuiBWtxCM84/8DdLhBVy",
        "linux_amd64": "sha384-0JuGGoQAqE1xTB0y1A15dRs96ALUyC/vUbnG8QyYt2i9JoNo4I0ez4VWvns23VCG",
        "linux_arm64": "sha384-4GK3AjqlyEq0OJdE+n0jqvShjFL4pKGmerjqVHfzg8crWX+Z7In01vF60u0/gbGo",
        "linux_ppc64le": "sha384-uuPHUoAC1m63DvkaMfgGT+DBy+jvWeP7lWqAepE18UmZo7jqI8VNQekkrBjlbcja",
        "linux_s390x": "sha384-gAaq3mNGy3sAAOUkqCqqkMTiKlEy0d1+pqf+1bJOFnBy/0JwTsCNOkz7JnaPP0Fr",
        "windows_amd64": "sha384-QbtaGOdRbZ3xDsPGWCijUliLzOU7Z1aYfUfC+UqJWmgy+iXxqEEvw9r7EZB1eLQK",
    },
}

def _controller_gen_toolchains_repo_impl(rctx):
    # Expose a concrete toolchain which is the result of Bazel resolving the toolchain
    # for the execution or target platform.
    # Workaround for https://github.com/bazelbuild/bazel/issues/14009
    defs_bzl = """# @generated by @io_github_janhicken_rules_kubebuilder//kubebuilder/private:controller_gen_toolchain.bzl

# Forward all the providers
def _resolved_toolchain_impl(ctx):
    toolchain_info = ctx.toolchains["@io_github_janhicken_rules_kubebuilder//kubebuilder:controller_gen_toolchain"]
    return [
        toolchain_info,
        toolchain_info.default,
        toolchain_info.controller_gen,
        toolchain_info.template_variables,
    ]

# Copied from java_toolchain_alias
# https://cs.opensource.google/bazel/bazel/+/master:tools/jdk/java_toolchain_alias.bzl
resolved_toolchain = rule(
    implementation = _resolved_toolchain_impl,
    toolchains = ["@io_github_janhicken_rules_kubebuilder//kubebuilder:controller_gen_toolchain"],
    incompatible_use_toolchain_transition = True,
)
"""
    rctx.file("defs.bzl", defs_bzl)

    build = """# @generated by @io_github_janhicken_rules_kubebuilder//kubebuilder/private:controller_gen_toolchain.bzl
#
# These can be registered in the workspace file or passed to --extra_toolchains flag.
# By default all these toolchains are registered by the controller_gen_register_toolchains macro
# so you don't normally need to interact with these targets.

load(":defs.bzl", "resolved_toolchain")

resolved_toolchain(name = "resolved_toolchain", visibility = ["//visibility:public"])

"""

    for [platform, meta] in CONTROLLER_GEN_PLATFORMS.items():
        build += """
toolchain(
    name = "{platform}_toolchain",
    exec_compatible_with = {compatible_with},
    toolchain = "@{user_repository_name}_{platform}//:controller_gen_toolchain",
    toolchain_type = "@io_github_janhicken_rules_kubebuilder//kubebuilder:controller_gen_toolchain",
)
""".format(
            platform = platform,
            user_repository_name = rctx.attr.user_repository_name,
            compatible_with = meta.compatible_with,
        )

    # Base BUILD file for this repository
    rctx.file("BUILD", build)

controller_gen_toolchains_repo = repository_rule(
    _controller_gen_toolchains_repo_impl,
    doc = """Creates a repository with toolchain definitions for all known platforms
     which can be registered or selected.""",
    attrs = {
        "user_repository_name": attr.string(doc = "Base name for toolchains repository"),
    },
)

def _controller_gen_platform_repo_impl(rctx):
    ext = ".exe" if "windows" in rctx.attr.platform else ""

    # https://github.com/kubernetes-sigs/controller-tools/releases/download/v0.17.2/controller-gen-darwin-arm64
    url = "https://github.com/kubernetes-sigs/controller-tools/releases/download/v{0}/controller-gen-{1}{2}".format(
        rctx.attr.version,
        rctx.attr.platform.replace("_", "-"),
        ext,
    )

    rctx.download(
        url = url,
        output = "controller-gen" + ext,
        executable = True,
        integrity = CONTROLLER_GEN_VERSIONS[rctx.attr.version][rctx.attr.platform],
    )
    build = """# @generated by @io_github_janhicken_rules_kubebuilder//kubebuilder/private:controller_gen_toolchain.bzl
load("@io_github_janhicken_rules_kubebuilder//kubebuilder:toolchain.bzl", "controller_gen_toolchain")
exports_files(["{0}"])
controller_gen_toolchain(name = "controller_gen_toolchain", bin = "{0}", visibility = ["//visibility:public"])
""".format("controller-gen" + ext)

    # Base BUILD file for this repository
    rctx.file("BUILD", build)

controller_gen_platform_repo = repository_rule(
    implementation = _controller_gen_platform_repo_impl,
    doc = "Fetch external tools needed for controller_gen toolchain",
    attrs = {
        "version": attr.string(mandatory = True, values = CONTROLLER_GEN_VERSIONS.keys()),
        "platform": attr.string(mandatory = True, values = CONTROLLER_GEN_PLATFORMS.keys()),
    },
)

def _controller_gen_host_alias_repo(rctx):
    ext = ".exe" if repo_utils.is_windows(rctx) else ""

    # Base BUILD file for this repository
    rctx.file("BUILD", """# @generated by @io_github_janhicken_rules_kubebuilder//kubebuilder/private:controller_gen_toolchain.bzl
package(default_visibility = ["//visibility:public"])
exports_files(["controller-gen{ext}"])
""".format(
        ext = ext,
    ))

    rctx.symlink("../{name}_{platform}/controller-gen{ext}".format(
        name = rctx.attr.name,
        platform = repo_utils.platform(rctx),
        ext = ext,
    ), "controller-gen{ext}".format(ext = ext))

controller_gen_host_alias_repo = repository_rule(
    _controller_gen_host_alias_repo,
    doc = """Creates a repository with a shorter name meant for the host platform, which contains
    a BUILD file that exports symlinks to the host platform's binaries
    """,
)
