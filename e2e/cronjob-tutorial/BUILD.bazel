load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@gazelle//:def.bzl", "gazelle")
load("@io_github_janhicken_rules_kubebuilder//kubebuilder:defs.bzl", "kind_env")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_load")
load("@tar.bzl", "tar")
load("//config:const.bzl", "CONTAINER_IMAGE_REPOSITORY", "CONTROLLER_NAME")

gazelle(name = "gazelle")

tar(
    name = "cmd_layer",
    srcs = ["//cmd"],
    compress = "zstd",
    mtree = ["manager uid=0 gid=0 mode=0755 time=0 type=file content=$(location //cmd)"],
)

oci_image(
    name = "manager",
    base = "@distroless_static",
    entrypoint = ["/manager"],
    exposed_ports = [
        "8080/tcp",
        "8081/tcp",
    ],
    tars = [":cmd_layer"],
    visibility = ["//visibility:public"],
)

oci_image_index(
    name = "manager_index",
    images = [":manager"],
    platforms = [
        "@rules_go//go/toolchain:linux_amd64",
        "@rules_go//go/toolchain:linux_arm64",
    ],
    visibility = ["//visibility:public"],
)

oci_load(
    name = "docker_load",
    format = "docker",
    image = "//:manager",
    repo_tags = [CONTAINER_IMAGE_REPOSITORY],
)

filegroup(
    name = "manager.tar",
    srcs = [":docker_load"],
    output_group = "tarball",
)

oci_load(
    name = "oci_load",
    format = "oci",
    image = "//:manager_index",
    repo_tags = [CONTAINER_IMAGE_REPOSITORY],
)

filegroup(
    name = "manager_index.tar",
    srcs = [":oci_load"],
    output_group = "tarball",
    visibility = ["//visibility:public"],
)

container_structure_test(
    name = "container_structure",
    size = "small",
    configs = [":container_structure.yaml"],
    driver = "tar",
    image = ":manager.tar",
)

kind_env(
    name = "kind_env",
    cluster_name = CONTROLLER_NAME,
    images = ["//:manager_index.tar"],
    kustomization = "//config/local",
    visibility = ["//test:__subpackages__"],
)
